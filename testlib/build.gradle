plugins {
	id 'java-library'
}
ext.artifactId = project.artifactIdTestLib
version = rootProject.spotlessChangelog.versionNext
apply from: rootProject.file('gradle/java-setup.gradle')

dependencies {
	api project(':lib')
	api files(project(':lib').sourceSets.sortPom.output.classesDirs)
	api "com.diffplug.durian:durian-core:${VER_DURIAN}"
	api "com.diffplug.durian:durian-testlib:${VER_DURIAN}"
	api "org.junit.jupiter:junit-jupiter:${VER_JUNIT}"
	api "org.assertj:assertj-core:${VER_ASSERTJ}"

	implementation "com.diffplug.durian:durian-io:${VER_DURIAN}"
	implementation "com.diffplug.durian:durian-collect:${VER_DURIAN}"
	implementation gradleTestKit()
}

// we'll hold the testlib to a low standard (prize brevity)
spotbugs { reportLevel = 'high' } // low|medium|high (low = sensitive to even minor mistakes)

test {
	useJUnitPlatform()
	if (JavaVersion.current().isCompatibleWith(JavaVersion.VERSION_16)) {
		// for GJF https://github.com/diffplug/spotless/issues/834
		def args = [
			'--add-exports=jdk.compiler/com.sun.tools.javac.api=ALL-UNNAMED',
			'--add-exports=jdk.compiler/com.sun.tools.javac.file=ALL-UNNAMED',
			'--add-exports=jdk.compiler/com.sun.tools.javac.parser=ALL-UNNAMED',
			'--add-exports=jdk.compiler/com.sun.tools.javac.tree=ALL-UNNAMED',
			'--add-exports=jdk.compiler/com.sun.tools.javac.util=ALL-UNNAMED'
		]
		jvmArgs args
	}
}

apply from: rootProject.file('gradle/special-tests.gradle')

javadoc {
	options.addStringOption('Xdoclint:none', '-quiet')
	options.addStringOption('Xwerror', '-quiet')
}

